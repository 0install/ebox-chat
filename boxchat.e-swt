def makeApplication(options, args) {
	require(options == [], `Unsupported options: $options`)
	require(args == [], `This program takes no arguments: $args`)

	def contactsShell := powerbox.makeShell()

	def capTPNetworking :Map := powerbox.fetchAuth("network", fn {
		def nw := contactsShell.requestCapTPNetworking()
		powerbox.setAuth("network", nw)
		nw
	})

	def makeAtomicFile := <import:org.erights.e.extern.persist.makeAtomicFile>

	def acceptCall(callerName :String, caller) {
		def remoteEnd
		def callShell := powerbox.makeShell()
		callShell.setText(`Call from $callerName`)
		def callGUI := <this:makeCallGUI>(callShell, callerName, caller)
		return callGUI.getLocalEnd()
	}

	def configFile := <data:serverConfig>
	<this:networking>(configFile, capTPNetworking["introducer"], println)

	def list := <this:makeContactsList>(capTPNetworking["introducer"], capTPNetworking["identityMgr"],
					    makeAtomicFile(<data>, "contacts.json"), acceptCall)
	def gui := <this:makeContactsGUI>(contactsShell, powerbox, list)

	# will continue when all windows are closed
	interp.blockAtTop()
}
